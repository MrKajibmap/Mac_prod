/*****************************************************************
*  ВЕРСИЯ:
*     $Id: a3a6a0bc125d726c45842deea2ab59fc827a0b92 $
*
******************************************************************
*  НАЗНАЧЕНИЕ:
*     Возвращает кол-во записей в таблице.
*     Если кол-во неизвестно, по умолчанию делает полный скан.
*     В случае ошибки возвращает пустое значение.
*
*  ПАРАМЕТРЫ:
*     mpData            +  имя входной таблицы
*     mpTableScan       -  делать ли полный скан, если кол-во записей не получено из метаданных
*                          по умолчанию Y (делать)
*
******************************************************************
*  Использует:
*     member_attr
*
*  Устанавливает макропеременные:
*     нет
*
******************************************************************
* Пример использования:
*      %local lmvObs;
*     %let lmvObs = %member_obs (mpData=sashelp.class);
*
******************************************************************
*  30-05-2013  Нестерёнок     Начальное кодирование
******************************************************************/

%macro member_obs (mpData=, mpTableScan=Y);
   %local lmvObs lmvAttrValue;
   %let lmvObs = ;
   %let lmvAttrValue = %member_attr (mpData=&mpData, mpAttr=NLOBS);

   %if not %is_blank(lmvAttrValue) %then %do;
      %let lmvObs = &lmvAttrValue;
      %if &lmvObs lt 0 and &mpTableScan = Y %then
         %let lmvObs = %member_attr (mpData=&mpData, mpAttr=NLOBSF);
   %end;

   /* Возвращаем результат */
   %do;&lmvObs%end;
%mend member_obs;
