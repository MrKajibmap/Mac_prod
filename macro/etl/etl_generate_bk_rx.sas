/*****************************************************************
*  ВЕРСИЯ:
*     $Id: fb4f150a59a23ffa08ff63d9e7d470471f40f7a6 $
*
******************************************************************
*  НАЗНАЧЕНИЕ:
*     Генерирует RX-выражение для проверки или разбора бизнес-ключа.
*
*  ПАРАМЕТРЫ:
*     mpTemplate              +  шаблон RX-выражения
*                                Имеет вид:
*                                {[CG]n} - см. {[CG]1:n}
*                                {Cm:n} - строка длиной от m до n знаков ASCII
*                                {Gm:n} - строка длиной от m до n печатаемых знаков
*                                {Xn} - hex-строка длиной n знаков
*                                {[NDZT]n} - число длиной n знаков
*
******************************************************************
*  Использует:
*     нет
*
*  Устанавливает макропеременные:
*     нет
*
*  Ограничения:
*     1.  Формат G будет корректно работать только в SBCS-окружении.
*     2.  Формат C допускает любые печатаемые знаки ASCII, т.е. от !(33) до ~(126).
*
******************************************************************
*  Пример использования:
*     %put %etl_generate_bk_rx(mpTemplate= TEST_{G3:5}_{N6}_SAMPLE) ;
*     выводит TEST_([[:graph:]]{3,5})_([[:digit:]]{6})_SAMPLE
*
******************************************************************
*  03-11-2015  Нестерёнок     Начальное кодирование
******************************************************************/

%macro etl_generate_bk_rx (
   mpTemplate              =
);
   %local lmvOut;
   %let lmvOut  =  &mpTemplate;
   %let lmvOut  =  %sysfunc(prxchange(%str(s/\{C(\d+)\}/([!-~]{1,$1})/),               -1, %superq(lmvOut) ));
   %let lmvOut  =  %sysfunc(prxchange(%str(s/\{C(\d+)\:(\d+)\}/([!-~]{$1,$2})/),       -1, %superq(lmvOut) ));
   %let lmvOut  =  %sysfunc(prxchange(%str(s/\{G(\d+)\}/([[:graph:]]{1,$1})/),         -1, %superq(lmvOut) ));
   %let lmvOut  =  %sysfunc(prxchange(%str(s/\{G(\d+)\:(\d+)\}/([[:graph:]]{$1,$2})/), -1, %superq(lmvOut) ));
   %let lmvOut  =  %sysfunc(prxchange(%str(s/\{X(\d+)\}/([0-9A-F]{$1})/),              -1, %superq(lmvOut) ));
   %let lmvOut  =  %sysfunc(prxchange(%str(s/\{[NDZT](\d+)\}/([[:digit:]]{$1})/),      -1, %superq(lmvOut) ));
   %do;%unquote(&lmvOut)%end;
%mend etl_generate_bk_rx;
